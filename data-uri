#!/usr/bin/env perl
use strict;
use warnings;
no warnings qw( once qw );

use Getopt::Std;
use File::Slurp;

getopts( 'sw:', \my %opt );

die "usage: $0 [-s [-w N]] [file]\n" if @ARGV > 1;

my ( $data, $type );

require File::Type;
my $ft = File::Type->new();

if ( @ARGV ) {
	my $filename = shift;
	$data = read_file $filename;
	$type = $ft->checktype_filename( $filename );
}
else {
	$data = read_file \*STDIN;
	$type = $ft->checktype_contents( $data );
}

require URI;
my $u = URI->new( 'data:' );

$u->media_type( $type );
$u->data( $data );

$_ = $u->as_string;

$\ = "\n";

if ( $opt{s} ) {
	my $w = $opt{w} || 72;
	s!\A(.*?,)!!s;
	print $1;
	print $1 while /(.{1,$w})/gs;
}
else {
	print;
}
