#!/usr/bin/perl -w
use strict;
use File::Basename qw( basename dirname );
use File::Spec::Functions qw( catfile rel2abs abs2rel );

sub run { my $r = system @_; exit $r if $r }

die <<"" if not @ARGV > 1;
usage: ${\basename $0} <file> [file file ...] <destination>
       moves file to destination and creates a symlink
       from its previous location to the destination

my $dst = pop @ARGV;

die <<"" if @ARGV > 1 and not -d $dst;
${\basename $0}: with more than one source, destination must be a directory

if ( my @fail = grep { not -f } @ARGV ) {
	die map "${\basename $0}: not a file: $_\n", @fail;
}

for my $src ( @ARGV ) {
	run mv => $src, $dst;

	my $fq_dst = -d $dst
		? catfile $dst, basename $src
		: $dst;

	$_ = rel2abs $_ for $fq_dst, $src;

	$fq_dst = abs2rel $fq_dst, dirname $src;

	run ln => -s => $fq_dst, $src;
}
